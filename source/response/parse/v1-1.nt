import {
  core.int.parse {parse-int},
  this.header.parse {header-parser},
  this.private.parse {_http-line-break, _is-status-code-number, _read-all, _skip-space, _symbol, _until-newline},
  this.response {Response, response},
  zonk.parse {report-unexpected-input, take-while, zonk},
}

define _status-code<c>: zonk(c, int) {
  function (k) {
    try status-code-text = take-while(_is-status-code-number)(k);
    let status-code-or-none on status-code-text = parse-int(status-code-text);
    let _ = status-code-text;
    match status-code-or-none {
    | Right(v) =>
      Right(v)
    | Left(_) =>
      report-unexpected-input("status code")
    }
  }
}

define response-parser<c>: zonk(c, response) {
  function (k) {
    try protocol-version = _symbol(k);
    let _ = protocol-version;
    try _ = _skip-space(k);
    try status-code = _status-code(k);
    try _ = _skip-space(k);
    try reason-phrase = _until-newline(k);
    let _ = reason-phrase;
    try _ = _http-line-break(k);
    try fields = header-parser()(k);
    try _ = _http-line-break(k);
    try body = _read-all(k);
    Right(
      Response of {
        status-code,
        fields,
        body,
      },
    )
  }
}
